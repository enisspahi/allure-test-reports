plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'io.qameta.allure' version "2.11.2"
}

group = 'com.enisspahi.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

def allureVersion = "2.24.0"
def aspectJVersion = "1.9.20.1"

configurations {
	agent {
		canBeResolved = true
		canBeConsumed = true
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
	testImplementation "io.qameta.allure:allure-junit5"
	testImplementation "io.qameta.allure:allure-rest-assured"
	testImplementation "io.qameta.allure:allure-assertj"
	testImplementation 'io.rest-assured:rest-assured:5.3.2'
	testImplementation 'org.assertj:assertj-core:3.24.2'
	agent "org.aspectj:aspectjweaver:$aspectJVersion"
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}

test {
	useJUnitPlatform()
	jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}

